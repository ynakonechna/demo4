resource "aws_vpc" "vpc" {
  cidr_block       = var.vpc_cidr_block
  enable_dns_hostnames = true
  enable_dns_support = true
  
  tags = merge(var.tags, { "Name" = "vpc-test" })
}

data "aws_availability_zones" "available" {}

resource "aws_internet_gateway" "gw" {
  vpc_id = aws_vpc.vpc.id

  tags = merge(var.tags, { "Name" = "gw" })
}

resource "aws_subnet" "public" {
    count = length(var.public_subnets)

    vpc_id = aws_vpc.vpc.id
    cidr_block = var.public_subnets[count.index]
    availability_zone = data.aws_availability_zones.available.names[count.index]
    map_public_ip_on_launch = true

    tags = merge(var.tags, {"Name" = format("public-%d", count.index), "Tier" = "Public"})
}

resource "aws_subnet" "private" {
    count = length(var.private_subnets)

    vpc_id = aws_vpc.vpc.id
    cidr_block = var.private_subnets[count.index]
    availability_zone = data.aws_availability_zones.available.names[count.index]
    map_public_ip_on_launch = false

    tags = merge(var.tags, {"Name" = format("private-%d", count.index), "Tier" = "Private"})
}

resource "aws_eip" "nat_eip" {
   
  domain   = "vpc"

  tags = merge(var.tags, {"Name" = "nat-eip"})
}

resource "aws_nat_gateway" "nat" {
    allocation_id = aws_eip.nat_eip.id
    subnet_id = aws_subnet.public[0].id
  
    tags = merge(var.tags,{ "Name" = "nat" })
}

resource "aws_route_table" "private" {
    vpc_id = aws_vpc.vpc.id
  
    tags = merge(var.tags, { "Name" = "private-route_table"})
}

resource "aws_route_table" "public" {
    vpc_id = aws_vpc.vpc.id
  
    tags = merge(var.tags, { "Name" = "public-route_table"})
}

resource "aws_route" "public_internet_gateway" {
    route_table_id = aws_route_table.public.id
    destination_cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.gw.id
}

resource "aws_route" "private_nat_gateway" {
    route_table_id = aws_route_table.private.id
    destination_cidr_block = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat.id
}

resource "aws_route_table_association" "public" {
  count = length(var.public_subnets)
  subnet_id = element(aws_subnet.public.*.id, count.index)
  route_table_id = aws_route_table.public.id
}

resource "aws_route_table_association" "private" {
  count = length(var.private_subnets)
  subnet_id = element(aws_subnet.private.*.id, count.index)
  route_table_id = aws_route_table.private.id
}